Public Function CNSValido(CNS As String) As Boolean

    'A função recebe um número de Carteira Nacional de Saúde,
    'vulgarmente conhecida como Cartão do SUS, com máscara.

    Dim CNSSemMascara As String
    Dim DV As String
    Dim Digito As Integer
    Dim Soma As Integer
    Dim Resto As Integer

    'Primeiro verifica se a string está completa.
    'Nesse caso, 15 dígitos do CNS mais 3 de máscara.
    If Len(CNS) < 18 Then
        CNSValido = False
        Exit Function
    End If

    'Depois remove a máscara. Nesse caso é composta de espaços.
    CNSSemMascara = Replace(CNS, " ", "")

    'Existem duas categorias de números de CNS.
    'Os iniciados com 1 ou 2 e os iniciados com 7, 8 ou 9.
    'O tratamento dado a elas é diferente.

    Select Case Mid(CNSSemMascara, 1, 1)
        Case 1 To 2
            'Soma os 11 primeiros dígitos, multiplicados pela posição decrescente.
            For p = 1 To 11
                Soma = Soma + (CInt(Mid(CNSSemMascara, p, 1)) * (16 - p))
            Next

            'Tira o resto da divisão por 11.
            Resto = Soma Mod 11
            Digito = 11 - Resto

            'Se o Digito der 11, considera como se fosse 0.
            If Digito = 11 Then
                Digito = 0
            End If

            'Isso aqui é doidera, mas...
            '...se o dígito for 10, adiciona 2 na soma e recalcula o DV.
            If Digito = 10 Then
                Soma = Soma + 2
                Resto = Soma Mod 11
                Digito = 11 - Resto
                DV = "1" & CStr(Digito)
            Else
                DV = "0" & CStr(Digito)
            End If

            'Compara com os dois últimos dígitos informados.
            If Mid(CNSSemMascara, 14, 2) <> DV Then
                CNSValido = False
                Exit Function
            End If

        Case 7 To 9
            'Soma os dígitos, multiplicados pelo posição decrescente.
            For p = 1 To 15
                Soma = Soma + (CInt(Mid(CNSSemMascara, p, 1)) * (16 - p))
            Next

            'Tira o resto da divisão por 11.
            Resto = Soma Mod 11

            'Se tiver resto, não é válido.
            If Resto <> 0 Then
                CNSValido = False
                Exit Function
            End If

        Case Else
            'Só por precaução, caso o número informado não comece com 1, 2, 7, 8 ou 9.
            CNSValido = False
            Exit Function

    End Select

    'Se nada disso falhar, CNS é válido.
    CNSValido = True

End Function